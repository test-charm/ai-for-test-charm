plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.testcharm'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', '2025.1.0')
    set('cucumberVersion', '6.10.2')
    set('jfactoryDal', '0.8.1-alpha.26')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    implementation "io.cucumber:cucumber-gherkin:${cucumberVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation("io.cucumber:cucumber-java:${cucumberVersion}")
    testImplementation("io.cucumber:cucumber-spring:${cucumberVersion}")
    testImplementation "com.github.leeonky:jfactory:${jfactoryDal}"
    testImplementation "com.github.leeonky:jfactory-cucumber:${jfactoryDal}"
    testImplementation "com.github.leeonky:DAL-java:${jfactoryDal}"
    testImplementation "com.github.leeonky:DAL-extension-basic:${jfactoryDal}"

}

configurations {
    providedRuntime
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber(type: JavaExec) {
    dependsOn assemble, testClasses
    mainClass = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    environment "SPRING_PROFILES_ACTIVE", "test"
    jvmArgs "-Dfile.encoding=UTF-8", "-Dsun.jnu.encoding=utf-8"
    args = ['--plugin', 'pretty',
            '--glue', 'com.testcharm',
            '--glue', 'com.github',
            project.hasProperty('file') ? project.file : 'src/test/resources']
}

tasks.bootJar { enabled = false }

